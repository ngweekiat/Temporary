Option Explicit

Sub Edge_DumpHTML_OnFirstFrameStopped()
    Dim b As AutomateBrowser
    Dim msg As Dictionary
    Dim html As String
    Dim start As Date
    Dim timeoutSeconds As Long
    
    Set b = New_AutomateBrowser
    
    b.launch whichBrowser:=browserType.Edge, _
             url:="https://en.wikipedia.org/wiki/Kitten", _
             killWithoutAsking:=True
    
    b.cdp.Page.enable
    
    timeoutSeconds = 20
    start = Now
    
    Do
        ' fill event queue
        b.cdp.peakMessage
        
        ' read one from queue
        Set msg = b.cdp.popMessageFromQueue
        If Not msg Is Nothing Then
            If msg.Exists("method") Then
                If msg("method") = "Page.frameStoppedLoading" Then
                    Debug.Print "DETECTED: Page.frameStoppedLoading"
                    Exit Do
                End If
            End If
        End If
        
        If DateDiff("s", start, Now) > timeoutSeconds Then
            Debug.Print "TIMEOUT: no frameStoppedLoading detected"
            Exit Do
        End If
        DoEvents
    Loop
    
    html = CStr(b.jsEval("document.documentElement.outerHTML"))
    Debug.Print "HTML length = " & Len(html)
    Debug.Print (html)
    b.Quit
End Sub


